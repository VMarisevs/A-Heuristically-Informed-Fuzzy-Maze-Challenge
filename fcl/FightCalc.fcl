FUNCTION_BLOCK Fight

	VAR_INPUT
		monster_strength : REAL;
		weapon_power : REAL;
		life_force : REAL;	
	END_VAR

	VAR_OUTPUT
		victory : REAL;
	END_VAR

	
	FUZZIFY monster_strength
		// 0 - 10
		TERM low := SIGM -4 2;
		TERM average := GBELL 2 3 4;
		TERM high := SIGM 4 7;
	END_FUZZIFY
	
	FUZZIFY weapon_power
		// 0 - 10
		TERM none := (0, 1) (1,0);
		TERM low := (1,0) (4,1) (6,0);
		TERM average := GAUSS 7 1;
		TERM high :=  (7,0) (9,1) (10,1);
	END_FUZZIFY
	
	FUZZIFY life_force
		// 0 - 100 health points
		TERM dead := (0,1)(20,0);
		TERM alive := SIGM 0.2 55;
	END_FUZZIFY

	DEFUZZIFY victory
		TERM poor := (0,1) (0.35,1) (0.7,0);
		TERM good := (0.7,0) (0.8,1) (0.9,0);
		TERM excellent := (0.9,0) (0.95,1) (1,1);
		METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
		DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
	END_DEFUZZIFY


	RULEBLOCK No1
		RULE 1 : IF monster_strength IS low OR weapon_power IS high THEN victory IS excellent;
		RULE 2 : IF monster_strength IS average AND weapon_power IS low THEN victory IS poor;
		RULE 3 : IF monster_strength IS average AND weapon_power IS average THEN victory IS good;
		RULE 4 : IF monster_strength IS high AND weapon_power IS NOT high THEN victory IS poor;
	END_RULEBLOCK

END_FUNCTION_BLOCK